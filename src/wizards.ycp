/**
 * File:	include/kerberos-client/wizards.ycp
 * Package:	Configuration of kerberos-client
 * Summary:	Wizards definitions
 * Authors:	Jiri Suchomel <jsuchome@suse.cz>
 *
 * $Id$
 */

{

textdomain "kerberos";

import "Wizard";

include "ui/common_popups.ycp";
include "ui/common_messages.ycp";
include "wizard/sequencer.ycp";

include "kerberos-client/dialogs.ycp";

/**
 * Whole configuration of kerberos-client
 * @return sequence result
 */
global define symbol KerberosSequence() ``{

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    string caption = _("Kerberos-client configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    any ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of kerberos-client but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
global define symbol KerberosAutoSequence() ``{
    string caption = _("Kerberos-client configuration");
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    any ret = MainSequence();

    UI::CloseDialog();
    return ret;
}

/**
 * Main workflow of the kerberos-client configuration
 * @return sequence result
 */
global define symbol MainSequence() ``{

    map aliases = $[
	"pam"	:       ``( PamDialog() ),
	"configure"	:   ``( ConfigureDialog() ),
    ];

    map sequence = $[
	"ws_start" : "pam",
	"pam" : $[
	    `abort	: `abort,
	    `next	: "configure",
	],
	"configure" : $[
	    `abort	: `abort,
	    `next	: `next
	],
    ];

    any ret = WizardSequencer(aliases, sequence);

    return ret;
}

/* EOF */
}
